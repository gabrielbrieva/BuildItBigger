apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        free {
            applicationId 'com.udacity.gradle.builditbigger.free'
        }
        paid {
            applicationId 'com.udacity.gradle.builditbigger.paid'
        }
    }
}

task runAppEngineWrapper (dependsOn: ":jokegce:appengineRun") {
    evaluationDependsOn ":jokegce"

    project(":jokegce") {
        appengine {
            daemon true
        }
    }
}

task stopAppEngineWrapper(dependsOn: ':jokegce:appengineStop'){
    mustRunAfter ':app:connectedCheck'
}

task connectedTestWrapper(dependsOn: ':app:connectedCheck') {
    mustRunAfter ':app:runAppEngineWrapper'
}


task runConnectedTest (dependsOn: [':app:connectedTestWrapper', ':app:runAppEngineWrapper', ':app:stopAppEngineWrapper']) {
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':joke')
    compile project(':jokeactivity')
    compile 'com.android.support:appcompat-v7:22.2.1'

    // Added for AdMob
    freeCompile 'com.google.android.gms:play-services:8.1.0'
}
